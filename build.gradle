plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'ch.bildspur'
version '0.6.2'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-bin.zip"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // xml -> not available in java 11
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'

    // processing
    testCompile group: 'org.processing', name: 'core', version: '3.3.6'
    testCompile group: 'org.jogamp.jogl', name: 'jogl-all-main', version: '2.3.2'
    testCompile group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version: '2.3.2'
}

configurations {
    jar.archiveName = "artnet4j-${version}.jar"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    archiveName = "artnet4j-${version}.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

signing {
    sign publishing.publications
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cansik/artnet4j")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }

        maven {
            name = "Maven"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.findProperty("nexusUsername")
                password = project.findProperty("nexusPassword")
            }
        }
    }
    afterEvaluate {
        publications {
            gpr(MavenPublication) {
                from(components.java)
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "ArtNet for Java and Processing"
                    description = "Art-Net DMX over IP library for Java and Processing."
                    url = "https://github.com/cansik/artnet4j"
                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'cansik'
                            name = 'bildspur'
                            email = 'hello@bildspur.ch'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:cansik/artnet4j.git'
                        url = 'https://github.com/cansik/artnet4j'
                    }
                }
            }
        }
    }
}